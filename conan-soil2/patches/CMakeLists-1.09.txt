#=======================================
#                PROJECT                
#=======================================

cmake_minimum_required(VERSION 3.0)
project(SOIL2 VERSION 1.09 LANGUAGES C)

#=======================================
#                SETTINGS               
#=======================================

set(SOIL2_TARGET_NAME ${PROJECT_NAME})

# Set the C standard.
set(CMAKE_C_STANDARD_REQUIRED ON)
set(C_STANDARD 11)

# Export all symbols on Windows.
if(MSVC OR MSVC90 OR MSVC10)
  set(MSVC ON)
  set(CMAKE_WINDOWS_EXPORT_ALL_SYMBOLS ON)
endif()

# Set standard installation directories.
set(SOIL2_RUNTIME_DESTINATION "bin")
set(SOIL2_LIBRARY_DESTINATION "lib")
set(SOIL2_ARCHIVE_DESTINATION "lib")
set(SOIL2_INCLUDES_DIR "include")
set(SOIL2_INCLUDES_DESTINATION "${SOIL2_INCLUDES_DIR}/${PROJECT_NAME}")

# Set CMake targets name, namespace and install directory.
set(SOIL2_CMAKE_TARGETS_EXPORT_NAME "${PROJECT_NAME}Targets")
set(SOIL2_CMAKE_TARGETS_NAMESPACE "${PROJECT_NAME}::")

# CMake config files name.
set(SOIL2_CMAKE_VERSION_CONFIG_FILE "${CMAKE_CURRENT_BINARY_DIR}/${PROJECT_NAME}ConfigVersion.cmake")
set(SOIL2_CMAKE_PROJECT_CONFIG_FILE "${CMAKE_CURRENT_BINARY_DIR}/${PROJECT_NAME}Config.cmake")

# CMake config files destination.
if(WIN32 AND NOT CYGWIN)
	set(SOIL2_CMAKE_CONFIG_DESTINATION "CMake")
else()
	set(SOIL2_CMAKE_CONFIG_DESTINATION "${SOIL2_LIBRARY_DESTINATION}/cmake/${PROJECT_NAME}")
endif()

#=======================================
#                LIBRARY                
#=======================================

# Store header files.
set(
	SOIL2_HEADER_FILES
	src/SOIL2/etc1_utils.h
	src/SOIL2/image_DXT.h
	src/SOIL2/image_helper.h
	src/SOIL2/pkm_helper.h
	src/SOIL2/pvr_helper.h
	src/SOIL2/SOIL2.h
	src/SOIL2/stbi_DDS.h
	src/SOIL2/stbi_DDS_c.h
	src/SOIL2/stbi_ext.h
	src/SOIL2/stbi_ext_c.h
	src/SOIL2/stbi_pkm.h
	src/SOIL2/stbi_pkm_c.h
	src/SOIL2/stbi_pvr.h
	src/SOIL2/stbi_pvr_c.h
	src/SOIL2/stb_image.h
	src/SOIL2/stb_image_write.h
	)

# Create the library (headers are here since some IDE need them here).
add_library(
	${SOIL2_TARGET_NAME} 
	src/SOIL2/etc1_utils.c
	src/SOIL2/image_DXT.c
	src/SOIL2/image_helper.c
	src/SOIL2/SOIL2.c
	${SOIL2_HEADER_FILES}
	)

# Add include directory (includes may not be at the same place between build and install).
target_include_directories(
    ${SOIL2_TARGET_NAME}
    PUBLIC
    $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/src>
    $<INSTALL_INTERFACE:${SOIL2_INCLUDES_DIR}>
)

#=======================================
#                PACKAGE                
#=======================================

find_package(OpenGL REQUIRED)
target_link_libraries(${SOIL2_TARGET_NAME} PUBLIC OpenGL::GL)

if(APPLE)
	target_link_libraries(${SOIL2_TARGET_NAME} PUBLIC "-framework CoreFoundation")
endif()

#=======================================
#                CONFIG                 
#=======================================

# Include write_basic_package_version_file and configure_package_config_file.
include(CMakePackageConfigHelpers)

# Generate basic config version files (SOIL2ConfigVersion.cmake).
write_basic_package_version_file(
    ${SOIL2_CMAKE_VERSION_CONFIG_FILE} 
    VERSION ${PROJECT_VERSION} 
    COMPATIBILITY SameMajorVersion
)

# Configure the config.cmake.in
configure_package_config_file(
	"${CMAKE_SOURCE_DIR}/SOIL2Config.cmake.in" ${SOIL2_CMAKE_PROJECT_CONFIG_FILE} 
	INSTALL_DESTINATION  ${SOIL2_CMAKE_CONFIG_DESTINATION}
	PATH_VARS SOIL2_INCLUDES_DESTINATION SOIL2_CMAKE_CONFIG_DESTINATION SOIL2_TARGET_NAME SOIL2_LIBRARY_DESTINATION
	)

# Install cmake config files (SOIL2ConfigVersion.cmake and SOIL2Config.cmake).
install(
    FILES ${SOIL2_CMAKE_VERSION_CONFIG_FILE} ${SOIL2_CMAKE_PROJECT_CONFIG_FILE}
    DESTINATION ${SOIL2_CMAKE_CONFIG_DESTINATION}
    )

# Install cmake targets files (SOIL2Targets.cmake and SOIL2Targets-debug.cmake).
install(
    EXPORT ${SOIL2_CMAKE_TARGETS_EXPORT_NAME}
    NAMESPACE ${SOIL2_CMAKE_TARGETS_NAMESPACE}
    DESTINATION ${SOIL2_CMAKE_CONFIG_DESTINATION}
    )

#=======================================
#                INSTALL                
#=======================================

# Install the library.
install(
	FILES ${SOIL2_HEADER_FILES} 
	DESTINATION ${SOIL2_INCLUDES_DESTINATION}
	)

install(
	TARGETS ${SOIL2_TARGET_NAME}
	EXPORT ${SOIL2_CMAKE_TARGETS_EXPORT_NAME}
	RUNTIME DESTINATION ${SOIL2_RUNTIME_DESTINATION}
	LIBRARY DESTINATION ${SOIL2_LIBRARY_DESTINATION}
	ARCHIVE DESTINATION ${SOIL2_ARCHIVE_DESTINATION}
	)